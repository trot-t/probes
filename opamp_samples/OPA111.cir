* OPA111
*****************************************************************************
* (C) COPYRIGHT 2011 TEXAS INSTRUMENTS INCORPORATED. ALL RIGHTS RESERVED.                                            
*****************************************************************************
** THIS MODEL IS DESIGNED AS AN AID FOR CUSTOMERS OF TEXAS INSTRUMENTS.
** TI AND ITS LICENSORS AND SUPPLIERS MAKE NO WARRANTIES, EITHER EXPRESSED
** OR IMPLIED, WITH RESPECT TO THIS MODEL, INCLUDING THE WARRANTIES OF 
** MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.  THE MODEL IS
** PROVIDED SOLELY ON AN "AS IS" BASIS.  THE ENTIRE RISK AS TO ITS QUALITY
** AND PERFORMANCE IS WITH THE CUSTOMER.
*****************************************************************************
*
* THIS MODEL IS SUBJECT TO CHANGE WITHOUT NOTICE. TEXAS INSTRUMENTS
* INCORPORATED IS NOT RESPONSIBLE FOR UPDATING THIS MODEL.
*
*****************************************************************************
*
** RELEASED BY: ANALOG ELAB DESIGN CENTER, TEXAS INSTRUMENTS INC.
* PART: OPA111
* DATE: 04MAY2011
* MODEL TYPE: ALL IN ONE
* SIMULATOR: TINA
* SIMULATOR VERSION: 9.0.10.194 SF
* EVM ORDER NUMBER: N/A 
* EVM USERS GUIDE: N/A
* DATASHEET: SBOS138
*
* MODEL VERSION: 1.0
*
*****************************************************************************
*
* UPDATES:
*
* VERSION 1.0 : 
* RELEASE TO WEB
*
*****************************************************************************
* CONNECTIONS:   NON-INVERTING INPUT
*                  | INVERTING INPUT
*                 | | POSITIVE POWER SUPPLY
*                 | | | NEGATIVE POWER SUPPLY
*           | | | | OUTPUT
*                 | | | | |
* PIN CONFIG FOR OPA111: 1 2 3 4 5
*****************************************************************************
.SUBCKT OPA111_0   1 2 3 4 5
C1   11 12 72.79E-12
C2    6  7 400.0E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 4.717E6 -5E6 5E6 5E6 -5E6
GA    6  0 11 12 5.027E-3
GCM   0  6 10 99 15.90E-9
ISS   3 10 DC 800.0E-6
HLIM 90  0 VLIM 1K
J1   11  2 10 JX
J2   12  64 10 JX
G11 2 4 POLY(4) (10,2) (11,2) (4,2) (66,0) 0 1E-12 1E-12 1E-12 400E-9
G21 1 4 POLY(4) (10,1) (12,1) (4,1) (68,0) 0 1E-12 1E-12 1E-12 400E-9
R2    6  9 100.0E3
RD1   4 11 198.9
RD2   4 12 198.9
RO1   8  5 25
RO2   7 99 75
*  RP    3  4 12.00E3
RSS  10 99 250.0E3
VB    9  0 DC 0
VC    3 53 DC 3
VE   54  4 DC 3
VLIM  7  8 DC 0
VLP  91  0 DC 40
VLN   0 92 DC 40

* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  1.30E-3  1
FQ2   0  4 POLY(1) VQ2  1.30E-3 -1

* QUIESCIENT CURRENT
RQ    3  4  7.5E4

* DIFF INPUT CAPACITANCE
CDIF  64  2  1.0E-12

* COMMON MODE INPUT CAPACITANCE
C1CM  64  99 1.5E-12
C2CM  2  99 1.5E-12

* INPUT VOLTAGE NOISE 
VN1 61 4  0.6
VN2 4  62 0.6
DN1 61 63 DY
DN2 63 62 DY
EN 64 1  63 4 1

* INPUT CURRENT NOISE
RN1 0 65 60.3865
RN2 65 66 60.3865
RN3 66 0 120.773
RN4 0 67 60.3865
RN5 67 68 60.3865
RN6 68 0 120.773

.MODEL DY D(IS=3.8E-16 AF=1 KF=1.175E-16)
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=400.0E-15 BETA=15.79E-3 VTO=-1)

.ENDS OPA111_0 

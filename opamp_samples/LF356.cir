*//////////////////////////////////////////////////////////////////////
* (C) NATIONAL SEMICONDUCTOR, INC.
* MODELS DEVELOPED AND UNDER COPYRIGHT BY:
* NATIONAL SEMICONDUCTOR, INC.

*/////////////////////////////////////////////////////////////////////
* LEGAL NOTICE: THIS MATERIAL IS INTENDED FOR FREE SOFTWARE SUPPORT.
* THE FILE MAY BE COPIED, AND DISTRIBUTED; HOWEVER, RESELLING THE
*  MATERIAL IS ILLEGAL

*////////////////////////////////////////////////////////////////////
* FOR ORDERING OR TECHNICAL INFORMATION ON THESE MODELS, CONTACT:
* NATIONAL SEMICONDUCTOR'S CUSTOMER RESPONSE CENTER
*                 7:00 A.M.--7:00 P.M.  U.S. CENTRAL TIME
*                                (800) 272-9959
* FOR APPLICATIONS SUPPORT, CONTACT THE INTERNET ADDRESS:
*  AMPS-APPS@GALAXY.NSC.COM
* ///////////////////////////////////////////////////////////////////
* USER NOTES:
*
* 1. INPUT RESISTANCE (RIN) FOR THESE JFET OP AMPS IS 1TOHM.  RIN IS
*    MODELED BY ASSUMING THE OPTION GMIN=1TOHM.  IF A DIFFERENT (NON-
*    DEFAULT) GMIN VALUE IS NEEDED, USERS MAY RECALCULATE AS FOLLOWS:
*    RIN=(R1||GMIN+R2||GMIN), WHERE R1=R2,
*    TO MAINTAIN A CONSISTENT RIN MODEL.

*//////////////////////////////////////////////////////////
*LF356 MONOLITHIC JFET-INPUT OP-AMP MACRO-MODEL
*//////////////////////////////////////////////////////////
*
* CONNECTIONS:    NON-INVERTING INPUT
*                 |   INVERTING INPUT
*                 |   |   POSITIVE POWER SUPPLY
*                 |   |   |   NEGATIVE POWER SUPPLY
*                 |   |   |   |   OUTPUT
*                 |   |   |   |   |
*                 |   |   |   |   |
.SUBCKT LF356_0      1   2  99  50  28
*
* PINOUT ORDER +IN -IN V+ V- OUT
*
*
*FEATURES:
*LOW INPUT BIAS CURRENT =             30PA
*LOW INPUT OFFSET CURRENT =            3PA
*HIGH INPUT IMPEDANCE =              1TOHM
*LOW INPUT OFFSET VOLTAGE =            1MV
*
****************INPUT STAGE**************
*
IOS 2 1 3P
*^INPUT OFFSET CURRENT
R1 1 3 1E12
R2 3 2 1E12
I1 99 4 100U
J1 5 2 4 JX
J2 6 7 4 JX
R3 5 50 20K
R4 6 50 20K
*FP2=20 MHZ
C4 5 6 1.9894E-13
*
***********COMMON MODE EFFECT***********
*
I2 99 50 4.65MA
*^QUIESCENT SUPPLY CURRENT
EOS 7 1 POLY(1) 16 49 3E-3 1
*INPUT OFFSET VOLTAGE.^
R8 99 49 50K
R9 49 50 50K
*
*********OUTPUT VOLTAGE LIMITING********
V2 99 8 2.63
D1 9 8 DX
D2 10 9 DX
V3 10 50 2.63
*
**************SECOND STAGE**************
*
EH 99 98 99 49 1
F1 9 98 POLY(1) VA3 0 0 0 1.5944E7
G1 98 9 5 6 2E-3
R5 98 9 100MEG
VA3 9 11 0
*FP1=31.96 HZ
C3 98 11 49.9798P
*
*********COMMON-MODE ZERO STAGE*********
*
G4 98 16 3 49 1E-8
L2 98 17 530.52M
R13 17 16 1K
*
**************OUTPUT STAGE**************
*
F6  99 50 VA7 1
F5  99 23 VA8 1
D5  21 23 DX
VA7 99 21 0
D6  23 99 DX
E1  99 26 99 9 1
VA8 26 27 0
R16 27 28 20
V5  28 25 -.25V
D4  25  9 DX
V4  24 28 -.25V
D3   9 24 DX
*
***************MODELS USED**************
*
.MODEL DX D(IS=1E-15)
.MODEL JX PJF(BETA=1.25E-5 VTO=-2.00 IS=30E-12)
*
.ENDS
